plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom'
    id 'com.gradleup.shadow' version '8.3.3'
}

base {
    archivesName = "${mod_id}-fabric-${minecraft_version}"
}

configurations {
    shade
}

repositories {
    flatDir { dirs '../libs' }
    maven { url "https://www.cursemaven.com" }
    maven { url "https://api.modrinth.com/maven/" }
//    maven { url 'https://jitpack.io' }
    maven {
        name = 'BlameJared'
        url = 'https://maven.blamejared.com'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${mapping_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    // Mod Menu
    modImplementation  "maven.modrinth:modmenu:${modmenu_version}"
    // MrCrayfish Config Mods
    modImplementation "curse.maven:catalogue-fabric-667377:4766089" // Catalogue
    // Title Screen Compatibility Mods
    modCompileOnly "curse.maven:aether-255308:6800393" // The Aether

    // exp4j (Expression Parsing)
    modImplementation "net.objecthunter:exp4j:0.4.8"
    shade "net.objecthunter:exp4j:0.4.8"
    // glsl preprocessor (shader patching)
    modImplementation "local_slim:glsl_parser:${glsl_parser_version}"
    shade ("local_slim:glsl_parser:${glsl_parser_version}")
    modImplementation "io.github.ocelot:glsl-processor:${glsl_processor_version}"
    shade ("io.github.ocelot:glsl-processor:${glsl_processor_version}") {
        exclude group: "org.ow2.asm"
    }
    // NightConfig (Config System)
    modImplementation "com.electronwill.night-config:core:3.8.1" // Core
    modImplementation "com.electronwill.night-config:toml:3.8.1" // Toml
    shade "com.electronwill.night-config:core:3.8.1"
    shade "com.electronwill.night-config:toml:3.8.1"

    // Performance Monitoring
    modRuntimeOnly "curse.maven:spark-361579:4738953" // Spark
    // Render Mods To Test Shader Patcher
//    modRuntimeOnly "curse.maven:indium-459496:6237037" // Indium
    modCompileOnly "curse.maven:sodium-394468:6260639" // Sodium
    modCompileOnly "curse.maven:irisshaders-455508:6258195" // Iris

    implementation project(":common")
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("fabric client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("fabric server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

shadowJar {
    configurations = [project.configurations.shade] // Limits shaded libs to what was specified by "shade"
    archiveClassifier = tasks.named("jar").flatMap { jar -> jar.archiveClassifier } // Ensures JAR names match so remap works
    destinationDirectory = tasks.named("jar").flatMap { jar -> jar.destinationDirectory } // Ensures output paths are consistent
    relocate 'net.objecthunter', "${project.group}.shadowed.objecthunter"
    relocate 'io.github.ocelot', "${project.group}.shadowed.github.ocelot"
    relocate 'com.electronwill.nightconfig', "${project.group}.shadowed.electronwill.nightconfig"
    relocate 'tfc.glsl', "${project.group}.shadowed.tfc.glsl"
    minimize {
        include(dependency("io.github.ocelot:.*:.*"))
        include(dependency("local_slim:.*:.*"))
    }
}

// Makes sure that the remapJar task depends on the shadowJar task.
// This means that the shadow JAR will be built before the remapJar task.
remapJar.dependsOn shadowJar